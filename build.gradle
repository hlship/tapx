description = "Collection of add-ons for Tapestry 5"

tapestryVersion = "5.2.5"

if (!project.hasProperty('deployUserName')) deployUserName = '*UNSET-USERNAME*'
if (!project.hasProperty('deployPassword')) deployPassword = '*UNSET-PASSWORD*'


subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy' // mostly for testing
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'project-report'
    
    sourceCompatibility = '1.5'
    targetCompatibility = '1.5'
    
    version = '1.1-SNAPSHOT'

    group = 'com.howardlewisship'
    
    repositories {
        mavenCentral()
               
        // All things JBoss/Javassist/Hibernate
        mavenRepo urls: "https://repository.jboss.org/nexus/content/repositories/releases/"
        
        // For the Kaptcha library
        mavenRepo urls: "http://howardlewisship.com/repository/"    
    }
    
    configurations {
        deployerJars
        provided
    }

    // See http://jira.codehaus.org/browse/GRADLE-784
    
    sourceSets {
      main { 
        compileClasspath += configurations.provided 
      }
    }
    
    dependencies {
        compile "org.apache.tapestry:tapestry-core:$tapestryVersion"        
        testCompile "org.apache.tapestry:tapestry-test:$tapestryVersion"        
        testCompile "org.easymock:easymock:3.0"
        groovy "org.codehaus.groovy:groovy-all:1.7.4"
        
        deployerJars "org.apache.maven.wagon:wagon-ftp:1.0-beta-2"        
    }
    
    test {
        useTestNG()
        
        options.suites("src/test/conf/testng.xml")
    }
    
    task sourcesJar(type: Jar, dependsOn:classes) {
       classifier = 'sources'
       from sourceSets.main.allSource
    }

    artifacts {
       archives sourcesJar
    }    

    
    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            // deployUserName and deployPassword should be specified in ~/.gradle/gradle.properties
            snapshotRepository(url: "ftp://howardlewisship.com/snapshot-repository") {
              authentication(userName: deployUserName, password: deployPassword)
            }
            
        }
    }
}