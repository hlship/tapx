description = "Collection of add-ons for Tapestry 5"

tapestryVersion = "5.2.4"

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy' // mostly for testing
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'project-report'
    
    sourceCompatibility = '1.5'
    targetCompatibility = '1.5'
    
    version = '1.1-SNAPSHOT'

    group = 'com.howardlewisship'
    
    repositories {
        mavenCentral()

        // All things JBoss/Javassist/Hibernate
        mavenRepo urls: "https://repository.jboss.org/nexus/content/repositories/releases/"

    }
    
    dependencies {
        compile "org.apache.tapestry:tapestry-core:$tapestryVersion"
        testCompile "org.apache.tapestry:tapestry-test:$tapestryVersion"
        testCompile "org.easymock:easymock:3.0"
        groovy "org.codehaus.groovy:groovy-all:1.7.4"
    }
    
    test {
        useTestNG()
        
        options.suites("src/test/conf/testng.xml")
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
       classifier = 'sources'
       from sourceSets.main.allSource
    }

    artifacts {
       archives sourcesJar
    }
}
