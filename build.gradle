description = "Collection of add-ons for Tapestry 5"

ext.tapestryVersion = "5.3.8"

ext.versions = [
    selenium: "2.35.0"
]

if (!project.hasProperty('deployUserName')) ext.deployUserName = '*UNSET-USERNAME*'
if (!project.hasProperty('deployPassword')) ext.deployPassword = '*UNSET-PASSWORD*'

apply plugin: 'idea'

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy' // mostly for testing
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'project-report'
    
    sourceCompatibility = '1.5'
    targetCompatibility = '1.5'
    
    version = '1.3'

    group = 'com.howardlewisship'
    
    repositories {
        mavenCentral()
               
        // All things JBoss/Javassist/Hibernate
        maven {
            name "JBoss" 
            url "https://repository.jboss.org/nexus/content/repositories/releases/"
        }
        
        // For the Kaptcha library
        maven {
            name "HLS"
            url "http://howardlewisship.com/repository/"
        }

    	// Latest preview packages

        maven { 
            name "Apache Staging" 
            url "https://repository.apache.org/content/groups/staging"
        }
    }
    
    configurations {
        deployerJars
        provided
    }

    // See http://jira.codehaus.org/browse/GRADLE-784

    sourceSets {
        main {
            compileClasspath += [configurations.provided]
        }
        test {
            compileClasspath += [configurations.provided]
            runtimeClasspath += [configurations.provided]
        }
    }

    idea.module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
    
    dependencies {
        compile "org.apache.tapestry:tapestry-core:$tapestryVersion"
        provided "org.mortbay.jetty:servlet-api:3.0.20100224"
        testCompile "org.easymock:easymock:3.0"
        testCompile "org.codehaus.groovy:groovy-all:2.4.3"
        testCompile "org.testng:testng:6.8.21"
        deployerJars "org.apache.maven.wagon:wagon-ftp:1.0-beta-2"        
    }
    
    test {
        useTestNG()
        
        options.suites("src/test/conf/testng.xml")
    }
    
    task sourcesJar(type: Jar, dependsOn:classes) {
       classifier = 'sources'
       from sourceSets.main.allSource
    }

    artifacts {
       archives sourcesJar
    }    

    
    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            // deployUserName and deployPassword should be specified in ~/.gradle/gradle.properties

            snapshotRepository(url: "ftp://howardlewisship.com/snapshot-repository") {
              authentication(userName: deployUserName, password: deployPassword)
            }
            
            repository(url: "ftp://howardlewisship.com/repository") {
              authentication(userName: deployUserName, password: deployPassword)
            }
        }
    }
    
    idea.module {
      jdkName = "inherited"
    }
}

idea.project {
  languageLevel = "1.6"
}
